# Maintainer: toha257 <toha257@gmail.com>

_target="arm-linux-gnueabihf"
pkgname=${_target}-glibc
pkgver=2.19
pkgrel=1
pkgdesc="GNU C Library (${_target})"
arch=(any)
url="http://www.gnu.org/software/libc/"
license=('GPL')
depends=("${_target}-linux-api-headers")
makedepends=("${_target}-gcc-stage2" 'gperf')
provides=("${_target}-glibc-headers=${pkgver}" ${_target}-eglibc)
conflicts=("${_target}-glibc-headers" "${_target}-eglibc")
replaces=("${_target}-glibc-headers")
options=(!strip 'staticlibs')
source=(http://ftp.gnu.org/gnu/glibc/glibc-${pkgver}.tar.bz2)
md5sums=('3ca28f5099257aade61b1adedb7e3401')

build() {
  cd "$srcdir/glibc-${pkgver}"

  # Don not build timezones
  sed -i 's/timezone rt/rt/' Makeconfig

  rm -rf build
  mkdir build
  cd build
  touch ../intl/plural.c

  export CFLAGS="-U_FORTIFY_SOURCE -mlittle-endian -O2"
  export CPPFLAGS="-U_FORTIFY_SOURCE -O2"
  unset LD_LIBRARY_PATH

  export BUILD_CC=gcc
  export CC=${_target}-gcc
  export CXX=${_target}-g++
  export AR=${_target}-ar
  export RANLIB=${_target}-ranlib

  ../configure \
    --target=${_target} \
    --host=${_target} \
    --build=${CHOST} \
    --prefix=/ \
    --with-headers=/usr/${_target}/include \
    --enable-kernel=3.14.1 \
    --enable-add-ons=libidn,ports,nptl \
    --enable-shared \
    --with-tls \
    --with-__thread \
    --without-cvs \
    --disable-profile \
    --without-gd \
    --enable-obsolete-rpc

  make
}

package() {
  cd "$srcdir/glibc-${pkgver}/build"

  make "install_root=${pkgdir}/usr/${_target}" install

  mkdir -p "${pkgdir}/usr/${_target}/usr"
  ln -s ../{include,lib} "${pkgdir}/usr/${_target}/usr"
  # Remove unneeded for compilation files
  rm -rf ${pkgdir}/usr/${_target}/{bin,sbin,etc,share,var}
}