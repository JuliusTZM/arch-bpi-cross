# Contributor: Alexander 'hatred' Drozdov <adrozdoff@gmail.com>
# Maintainer: toha257 <toha257@gmail.com>

_target="arm-linux-gnueabihf"
pkgname=${_target}-glibc-headers
pkgver=2.20
pkgrel=0.1
pkgdesc="GNU C Library. Stage 1 - headers only. Needed for initialy build toolchain (${_target})"
arch=(any)
url="http://www.gnu.org/software/libc/"
license=('GPL')
depends=("${_target}-linux-api-headers")
makedepends=("${_target}-gcc-stage1")
options=(!strip)
source=(http://ftp.gnu.org/gnu/glibc/glibc-${pkgver}.tar.bz2)
md5sums=('5d9d375e670fe546a030061db36e9f3d')

build() {
  cd "$srcdir"
  cd glibc-${pkgver}

  # Don not build timezones
  sed -i 's/timezone rt/rt/' Makeconfig

  rm -rf build
  mkdir build
  cd build

  export CFLAGS="-U_FORTIFY_SOURCE  -mlittle-endian -O2"
  unset LD_LIBRARY_PATH

  export BUILD_CC=gcc
  export CC=${_target}-gcc
  export AR=${_target}-ar
  export RANLIB=${_target}-ranlib

  ../configure \
    --target=${_target} \
    --host=${_target} \
    --build=${CHOST} \
    --prefix=/  \
    --libexecdir=/lib \
    --with-headers=/usr/${_target}/include \
    --enable-add-ons \
    --disable-profile \
    --without-gd \
    --without-cvs

  # make some libs and stubs
  make csu/subdir_lib
}

package() {
  cd "$srcdir/glibc-$pkgver/build"

  make install-headers "install_root=${pkgdir}/usr/${_target}" \
    install-bootstrap-headers=yes

  # glibc doesn't support eglibc's install-bootstrap-headers
  # Luckily a blank gnu/stubs.h is all we need
  touch "${pkgdir}/usr/${_target}/include/gnu/stubs.h"

  mkdir -p "${pkgdir}/usr/${_target}/lib"
  cp csu/crt1.o csu/crti.o csu/crtn.o "${pkgdir}/usr/${_target}/lib"

  # create stub lib
  ${_target}-gcc -nostdlib -nostartfiles -shared -x c /dev/null \
    -o "${pkgdir}/usr/${_target}/lib/libc.so"

  mkdir -p "${pkgdir}/usr/${_target}/usr"
  ln -s ../{include,lib} "${pkgdir}/usr/${_target}/usr"
}
